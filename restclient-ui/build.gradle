plugins {
  id "edu.sc.seis.macAppBundle" version "2.1.6"
}

apply plugin: 'java'
apply plugin: 'application'

// Application plugin config:
mainClassName = 'org.wiztools.restclient.ui.Main'
applicationName = 'restclient'
applicationDefaultJvmArgs = ['-Xms128m', '-Xmx512m']

macAppBundle {
  mainClassName = 'org.wiztools.restclient.ui.Main'
  icon = "src/main/app-resources/rest-client_30.icns"
  bundleJRE = true
  javaProperties.put("apple.laf.useScreenMenuBar", "true")
  bundleIdentifier = 'org.wiztools.restclient.ui'
  appName = 'WizTools.org RESTClient'
  // backgroundImage = "doc/macbackground.png"
}

repositories {
  mavenLocal()
  mavenCentral()
}

task fatJar(type: Jar) {
  baseName = project.name + '-fat'

  manifest {
    attributes(
      "Main-Class": "$mainClassName",
      "SplashScreen-Image": 'org/wiztools/restclient/Splash.png'
    )
  }

  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }
  with jar
}

dependencies {
  compile project(':restclient-lib'),
    'org.wiztools:filechooser-abstraction:0.1.0',
    'com.jidesoft:jide-oss:3.6.14',
    'org.swinglabs.swingx:swingx-autocomplete:1.6.5-1',
    'com.fifesoft:rsyntaxtextarea:2.5.8',
    'org.simplericity.macify:macify:1.6'
  testCompile 'junit:junit:4.+'
}
